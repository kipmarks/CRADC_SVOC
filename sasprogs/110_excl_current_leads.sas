/**********************************************************************
Project: 	  Data & Intelligence Platform - Heritage

Program Name: 110_excl_current_leads.sas

Overview:     
              
***********************************************************************
                  DEPENDENCIES AND LIMITATIONS
      
 
Parameter(s):  None

Dependencies:  None


***********************************************************************
                    CHANGE CONTROL LOG 

Change log (in reverse chronological order) :-

June2019  	KM  Migration to DIP
2015-2019  	Original EXCL development on Heritage Platform by
            Richard Holley and members of the Analytics Team.
            	 
                                      
***********************************************************************/
/*********************************************************************************************************************************/
PROC SORT DATA=TDW_CURR.TBL_NZ_CAMPAIGN (KEEP = DOC_KEY CAMPAIGN_NAME GROUP_FLD CURRENT_REC_FLAG 
                                WHERE = (CURRENT_REC_FLAG = 'Y'))
OUT=TBL_NZ_CAMPAIGN;
BY DOC_KEY;RUN;
/*********************************************************************************************************************************/
PROC SORT DATA=TDW_CURR.TBL_LEAD (KEEP = IRD_NUMBER CUSTOMER_KEY LEAD_KEY LEAD_CATEGORY LEAD_TYPE LEAD_WORK_AREA DOC_KEY CREATED CLOSED CLOSE_REASON CURRENT_REC_FLAG
							     WHERE = (CURRENT_REC_FLAG = 'Y' AND LEAD_CATEGORY IN ('CMPAGN', 'INT') AND LEAD_TYPE NE 'ASOFCR' AND (CLOSED = . OR (CLOSED NE . AND TODAY() - DATEPART(CLOSED)<90))))
                                   OUT = TBL_LEAD;
BY DOC_KEY;RUN;
/*********************************************************************************************************************************/
PROC SORT DATA=TDW_CURR.TBL_LEDCCACTIONS (KEEP = DOC_KEY IRD_NUMBER CUSTOMER_KEY PORTFOLIO CAMPAIGN_NAME CURRENT_REC_FLAG 
                                         WHERE = (CURRENT_REC_FLAG = 'Y')) OUT=TBL_LEDCCACTIONS; 
BY DOC_KEY;
RUN;
/*********************************************************************************************************************************/
/* Current/Recently closed Leads
/*********************************************************************************************************************************/
DATA COMPLETE
     CURRENT_LEADS_RET    (KEEP = IRD_NUMBER CUSTOMER_KEY CREATED_DATE LEAD_TYPE_RET CURRENT_CAMPAIGN_PORTFOLIO_RET CURRENTLY_CAMPAIGNED_RET)
     CURRENT_LEADS_COL    (KEEP = IRD_NUMBER CUSTOMER_KEY CREATED_DATE LEAD_TYPE_COL CURRENT_CAMPAIGN_PORTFOLIO_COL CURRENTLY_CAMPAIGNED_COL)
     CURRENT_LEADS_CS     (KEEP = IRD_NUMBER CUSTOMER_KEY CREATED_DATE LEAD_TYPE_CS  CURRENT_CAMPAIGN_PORTFOLIO_CS  CURRENTLY_CAMPAIGNED_CS)
     CURRENT_LEADS_CC     (KEEP = IRD_NUMBER CUSTOMER_KEY CREATED_DATE LEAD_TYPE_CC  CURRENT_CAMPAIGN_PORTFOLIO_CC  CURRENTLY_CAMPAIGNED_CC)
     CURRENT_LEADS_INV    (KEEP = IRD_NUMBER CUSTOMER_KEY CREATED_DATE LEAD_TYPE_INV CURRENT_CAMPAIGN_PORTFOLIO_INV CURRENTLY_CAMPAIGNED_INV)
      CLOSED_LEADS_RET    (KEEP = IRD_NUMBER CUSTOMER_KEY CLOSED_LEAD_RET CLOSED_DATE_RET DAYS_SINCE_CLOSED_RET CLOSED_CAMPAIGN_PORTFOLIO_RET CLOSED_CAMPAIGNED_RET)
      CLOSED_LEADS_COL    (KEEP = IRD_NUMBER CUSTOMER_KEY CLOSED_LEAD_COL CLOSED_DATE_COL DAYS_SINCE_CLOSED_COL CLOSED_CAMPAIGN_PORTFOLIO_COL CLOSED_CAMPAIGNED_COL)
      CLOSED_LEADS_CS     (KEEP = IRD_NUMBER CUSTOMER_KEY CLOSED_LEAD_CS  CLOSED_DATE_CS  DAYS_SINCE_CLOSED_CS  CLOSED_CAMPAIGN_PORTFOLIO_CS  CLOSED_CAMPAIGNED_CS)
      CLOSED_LEADS_CC     (KEEP = IRD_NUMBER CUSTOMER_KEY CLOSED_LEAD_CC  CLOSED_DATE_CC  DAYS_SINCE_CLOSED_CC  CLOSED_CAMPAIGN_PORTFOLIO_CC  CLOSED_CAMPAIGNED_CC)
      CLOSED_LEADS_INV    (KEEP = IRD_NUMBER CUSTOMER_KEY CLOSED_LEAD_INV CLOSED_DATE_INV DAYS_SINCE_CLOSED_INV CLOSED_CAMPAIGN_PORTFOLIO_INV CLOSED_CAMPAIGNED_INV)
             LEADS_WTF; 

/*********************************************************************************************************************************/
/*  The reason we don't want info on ASOFCR is because they are associated leads, but they very rarely close, so we need to be 
/*  able to target these customers haven't included Anon leads at the moment, because if an anon lead is going to be progressed 
/*  the lead would be closed down and an investigation or CCOM lead would be opened up
/*********************************************************************************************************************************/
MERGE 	TBL_LEAD                  (IN = A)
		TBL_LEDCCACTIONS          (IN = B)
		TBL_NZ_CAMPAIGN           (IN = C RENAME = (GROUP_FLD = PORTFOLIO));
BY DOC_KEY;
IF A;


CREATED_DATE = DATEPART(CREATED);  FORMAT CREATED_DATE DATE9.;
CLOSED1      = DATEPART(CLOSED);   FORMAT CLOSED1      DATE9.;

/*Any leads for DHS Withdrawals should be ignored*/
IF INDEX(CAMPAIGN_NAME,'CS_INT_DHS_WD') > 0 THEN DELETE; 
/****************/
/*  Open Leads
/****************/
IF CLOSED = . AND CLOSE_REASON = '' THEN DO;
    IF LEAD_TYPE IN ('CCSRTN','RTCAMP','CCCAMP') AND LEAD_CATEGORY ne 'INT' THEN DO;
        LEAD_TYPE_RET = 'Y';
        CURRENT_CAMPAIGN_PORTFOLIO_RET = PORTFOLIO;
        CURRENTLY_CAMPAIGNED_RET = CAMPAIGN_NAME;
        OUTPUT CURRENT_LEADS_RET;
        END;
    ELSE IF (LEAD_TYPE IN ('CCCOL','DBCAMP') AND PORTFOLIO IN ('ONE','BAU','B12')) OR PORTFOLIO IN ('SL_DOM','SL_INT') THEN DO;
        LEAD_TYPE_COL = 'Y';
        CURRENT_CAMPAIGN_PORTFOLIO_COL = PORTFOLIO;
        CURRENTLY_CAMPAIGNED_COL = CAMPAIGN_NAME;
        OUTPUT CURRENT_LEADS_COL;
        END;
    ELSE IF LEAD_TYPE IN ('CCCOL','SPCAMP') AND PORTFOLIO IN ('CS_DOM', 'CS_INT', 'B14_CS') THEN DO;
        LEAD_TYPE_CS = 'Y';
        CURRENT_CAMPAIGN_PORTFOLIO_CS = PORTFOLIO;
        CURRENTLY_CAMPAIGNED_CS = CAMPAIGN_NAME;
        OUTPUT CURRENT_LEADS_CS;
        END;
	/*the call centre occasionally load leads under Collections, so additional OR statement to pick this up*/
    ELSE IF LEAD_TYPE IN ('CCCNTR','CLCAMP') OR (LEAD_TYPE IN ('CCCOL','DBCAMP') AND PORTFOLIO IN ('CC', 'CCNTR')) THEN DO;
        LEAD_TYPE_CC = 'Y';
        CURRENT_CAMPAIGN_PORTFOLIO_CC = PORTFOLIO;
        CURRENTLY_CAMPAIGNED_CC = CAMPAIGN_NAME;
        OUTPUT CURRENT_LEADS_CC;
        END;
	ELSE IF LEAD_CATEGORY = 'INT' THEN DO;
        LEAD_TYPE_INV = 'Y';
        CURRENT_CAMPAIGN_PORTFOLIO_INV = PORTFOLIO;
        CURRENTLY_CAMPAIGNED_INV = CAMPAIGN_NAME;
        OUTPUT CURRENT_LEADS_INV;
        END;
    ELSE OUTPUT LEADS_WTF;
        
    END;
/***************/
/*  Open Leads
/***************/
IF CLOSED >0 THEN DO;
    IF LEAD_TYPE IN ('CCSRTN','RTCAMP','CCCAMP') AND LEAD_CATEGORY ne 'INT' THEN DO;
        CLOSED_LEAD_RET = 'Y';
        CLOSED_CAMPAIGN_PORTFOLIO_RET = PORTFOLIO;
        CLOSED_CAMPAIGNED_RET = CAMPAIGN_NAME;
        CLOSED_DATE_RET = CLOSED1; FORMAT CLOSED_DATE_RET DATE9.;
        DAYS_SINCE_CLOSED_RET = (TODAY()- CLOSED_DATE_RET);
        OUTPUT CLOSED_LEADS_RET;
    	END;

    ELSE IF (LEAD_TYPE IN ('CCCOL','DBCAMP') AND PORTFOLIO IN ('ONE','BAU','B12')) OR PORTFOLIO IN ('SL_DOM','SL_INT') THEN DO;
		CLOSED_LEAD_COL = 'Y';
        CLOSED_CAMPAIGN_PORTFOLIO_COL = PORTFOLIO;
        CLOSED_CAMPAIGNED_COL = CAMPAIGN_NAME;
        CLOSED_DATE_COL = CLOSED1; FORMAT CLOSED_DATE_COL DATE9.;
        DAYS_SINCE_CLOSED_COL = (TODAY()- CLOSED_DATE_COL);
        OUTPUT CLOSED_LEADS_COL;
        END;
    ELSE IF LEAD_TYPE IN ('CCCOL','SPCAMP') AND PORTFOLIO IN ('CS_DOM', 'CS_INT', 'B14_CS') THEN DO;
        CLOSED_LEAD_CS = 'Y';
        CLOSED_CAMPAIGN_PORTFOLIO_CS = PORTFOLIO;
        CLOSED_CAMPAIGNED_CS = CAMPAIGN_NAME;
        CLOSED_DATE_CS = CLOSED1;  FORMAT CLOSED_DATE_CS DATE9.;
        DAYS_SINCE_CLOSED_CS = (TODAY()- CLOSED_DATE_CS);
        OUTPUT CLOSED_LEADS_CS;
        END;
    ELSE IF LEAD_TYPE IN ('CCCNTR','CLCAMP') OR (LEAD_TYPE IN ('CCCOL','DBCAMP') AND PORTFOLIO IN ('CC', 'CCNTR')) THEN DO;
        CLOSED_LEAD_CC = 'Y';
        CLOSED_CAMPAIGN_PORTFOLIO_CC = PORTFOLIO;
        CLOSED_CAMPAIGNED_CC = CAMPAIGN_NAME;
        CLOSED_DATE_CC = CLOSED1;  FORMAT CLOSED_DATE_CC DATE9.;
        DAYS_SINCE_CLOSED_CC = (TODAY()- CLOSED_DATE_CC);
        OUTPUT CLOSED_LEADS_CC;
        END;
	ELSE IF LEAD_CATEGORY = 'INT' THEN DO;
        CLOSED_LEAD_INV = 'Y';
        CLOSED_CAMPAIGN_PORTFOLIO_INV = PORTFOLIO;
        CLOSED_CAMPAIGNED_INV = CAMPAIGN_NAME;
        CLOSED_DATE_INV = CLOSED1;  FORMAT CLOSED_DATE_INV DATE9.;
        DAYS_SINCE_CLOSED_INV = (TODAY()- CLOSED_DATE_INV);
        OUTPUT CLOSED_LEADS_INV;
        END;
    ELSE OUTPUT LEADS_WTF;
    END;

RUN;

/*********************************************************************************************************************************/
/*  Sort 'em out
/*********************************************************************************************************************************/
PROC SORT DATA = CURRENT_LEADS_RET; BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_RET;   RUN;
PROC SORT DATA = CURRENT_LEADS_COL; BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_COL;   RUN;
PROC SORT DATA = CURRENT_LEADS_CS;  BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_CS;    RUN;
PROC SORT DATA = CURRENT_LEADS_CC;  BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_CC;    RUN;
PROC SORT DATA = CURRENT_LEADS_INV; BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_INV;   RUN;

PROC SORT DATA = CLOSED_LEADS_RET;  BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_RET CLOSED_CAMPAIGN_PORTFOLIO_RET; RUN;
PROC SORT DATA = CLOSED_LEADS_COL;  BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_COL CLOSED_CAMPAIGN_PORTFOLIO_COL; RUN;
PROC SORT DATA = CLOSED_LEADS_CS;   BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_CS  CLOSED_CAMPAIGN_PORTFOLIO_CS;  RUN;
PROC SORT DATA = CLOSED_LEADS_CC;   BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_CC  CLOSED_CAMPAIGN_PORTFOLIO_CC;  RUN;
PROC SORT DATA = CLOSED_LEADS_INV;  BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_INV CLOSED_CAMPAIGN_PORTFOLIO_INV; RUN;

/*********************************************************************************************************************************/
/* Extract prioritised current/closed lead records for each work area 
/*********************************************************************************************************************************/
DATA CURRENT_LEADS_RET; SET CURRENT_LEADS_RET; BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_RET; IF LAST.IRD_NUMBER; RUN;
DATA CURRENT_LEADS_COL; SET CURRENT_LEADS_COL; BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_COL; IF LAST.IRD_NUMBER; RUN;
DATA CURRENT_LEADS_CS;  SET CURRENT_LEADS_CS;  BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_CS;  IF LAST.IRD_NUMBER; RUN;
DATA CURRENT_LEADS_CC;  SET CURRENT_LEADS_CC;  BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_CC;  IF LAST.IRD_NUMBER; RUN;
DATA CURRENT_LEADS_INV; SET CURRENT_LEADS_INV; BY IRD_NUMBER CUSTOMER_KEY CREATED_DATE CURRENT_CAMPAIGN_PORTFOLIO_INV; IF LAST.IRD_NUMBER; RUN;

DATA CLOSED_LEADS_RET; SET CLOSED_LEADS_RET; BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_RET CLOSED_CAMPAIGN_PORTFOLIO_RET; IF LAST.IRD_NUMBER; RUN;
DATA CLOSED_LEADS_COL; SET CLOSED_LEADS_COL; BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_COL CLOSED_CAMPAIGN_PORTFOLIO_COL; IF LAST.IRD_NUMBER; RUN;
DATA CLOSED_LEADS_CS;  SET CLOSED_LEADS_CS;  BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_CS  CLOSED_CAMPAIGN_PORTFOLIO_CS;  IF LAST.IRD_NUMBER; RUN;
DATA CLOSED_LEADS_CC;  SET CLOSED_LEADS_CC;  BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_CC  CLOSED_CAMPAIGN_PORTFOLIO_CC;  IF LAST.IRD_NUMBER; RUN;
DATA CLOSED_LEADS_INV; SET CLOSED_LEADS_INV; BY IRD_NUMBER CUSTOMER_KEY CLOSED_DATE_INV CLOSED_CAMPAIGN_PORTFOLIO_INV; IF LAST.IRD_NUMBER; RUN;

 DATA EXCLTEMP.LEADS_TOTAL (DROP = CREATED_DATE);
MERGE CURRENT_LEADS_RET CURRENT_LEADS_COL CURRENT_LEADS_CS CURRENT_LEADS_CC CURRENT_LEADS_INV 
       CLOSED_LEADS_RET  CLOSED_LEADS_COL  CLOSED_LEADS_CS  CLOSED_LEADS_CC  CLOSED_LEADS_INV;
BY IRD_NUMBER CUSTOMER_KEY ;
RUN;
/*********************************************************************************************************************************/
/* Dataset with all currently open leads across work areas 
/*********************************************************************************************************************************/
DATA EXCLTEMP.LEADS_ALL_CURRENT;    /* NOT DROPPING CREATED_DATE AS RECORDS EXIST WITH SAME CAMPAIGN_NAME AND DIFF CREATED_DATE */
      SET CURRENT_LEADS_RET (DROP = LEAD_TYPE_RET RENAME = (CURRENTLY_CAMPAIGNED_RET = CAMPAIGN_NAME CURRENT_CAMPAIGN_PORTFOLIO_RET = PORTFOLIO))
          CURRENT_LEADS_COL (DROP = LEAD_TYPE_COL RENAME = (CURRENTLY_CAMPAIGNED_COL = CAMPAIGN_NAME CURRENT_CAMPAIGN_PORTFOLIO_COL = PORTFOLIO))
          CURRENT_LEADS_CS  (DROP = LEAD_TYPE_CS  RENAME = (CURRENTLY_CAMPAIGNED_CS  = CAMPAIGN_NAME CURRENT_CAMPAIGN_PORTFOLIO_CS  = PORTFOLIO))
          CURRENT_LEADS_CC  (DROP = LEAD_TYPE_CC  RENAME = (CURRENTLY_CAMPAIGNED_CC  = CAMPAIGN_NAME CURRENT_CAMPAIGN_PORTFOLIO_CC  = PORTFOLIO))
          CURRENT_LEADS_INV (DROP = LEAD_TYPE_INV RENAME = (CURRENTLY_CAMPAIGNED_INV = CAMPAIGN_NAME CURRENT_CAMPAIGN_PORTFOLIO_INV = PORTFOLIO))
      INDSNAME = DSN;
LEAD_GROUP = SCAN (DSN, 3, '_');
RUN;

/************************************************************************************************************************/
/* Dataset with all closed leads across work areas 
/************************************************************************************************************************/
DATA EXCLTEMP.LEADS_ALL_CLOSED;   
      SET CLOSED_LEADS_RET (DROP = CLOSED_LEAD_RET RENAME = (CLOSED_DATE_RET = CLOSED_DATE DAYS_SINCE_CLOSED_RET = DAYS_SINCE_CLOSED CLOSED_CAMPAIGNED_RET = CAMPAIGN_NAME CLOSED_CAMPAIGN_PORTFOLIO_RET = PORTFOLIO))
          CLOSED_LEADS_COL (DROP = CLOSED_LEAD_COL RENAME = (CLOSED_DATE_COL = CLOSED_DATE DAYS_SINCE_CLOSED_COL = DAYS_SINCE_CLOSED CLOSED_CAMPAIGNED_COL = CAMPAIGN_NAME CLOSED_CAMPAIGN_PORTFOLIO_COL = PORTFOLIO))
          CLOSED_LEADS_CS  (DROP = CLOSED_LEAD_CS  RENAME = (CLOSED_DATE_CS  = CLOSED_DATE DAYS_SINCE_CLOSED_CS  = DAYS_SINCE_CLOSED CLOSED_CAMPAIGNED_CS  = CAMPAIGN_NAME CLOSED_CAMPAIGN_PORTFOLIO_CS  = PORTFOLIO))
          CLOSED_LEADS_CC  (DROP = CLOSED_LEAD_CC  RENAME = (CLOSED_DATE_CC  = CLOSED_DATE DAYS_SINCE_CLOSED_CC  = DAYS_SINCE_CLOSED CLOSED_CAMPAIGNED_CC  = CAMPAIGN_NAME CLOSED_CAMPAIGN_PORTFOLIO_CC  = PORTFOLIO))
          CLOSED_LEADS_INV (DROP = CLOSED_LEAD_INV RENAME = (CLOSED_DATE_INV = CLOSED_DATE DAYS_SINCE_CLOSED_INV = DAYS_SINCE_CLOSED CLOSED_CAMPAIGNED_INV = CAMPAIGN_NAME CLOSED_CAMPAIGN_PORTFOLIO_INV = PORTFOLIO))
      INDSNAME = DSN;
LEAD_GROUP = SCAN (DSN, 3, '_');
RUN; 
/*********************************************************************************************************************************/
PROC DATASETS LIB = WORK NOLIST; DELETE CURRENT_LEADS_: CLOSED_LEADS_: ; RUN;
/*********************************************************************************************************************************/
